apply plugin: 'com.android.application'

ext {
    /* Version code for *next* release, bump *after* a release is created. */
    // 1 or more digits
    versionMajor = 4
    // exactly 1 digit
    versionMinor = 0
    // exactly 2 digits
    versionBuild = 06
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"

    defaultConfig {
        applicationId "com.afwsamples.testdpc"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode versionMajor * 1000 + versionMinor * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
        maven {
            url 'https://maven.google.com'
        }
    }

    // Enable lint checking in all build variants.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            output.assemble.dependsOn lintTask
        }
    }
}

dependencies {
    api 'com.android.support:preference-v14:27.0.2'
    api 'com.android.support:recyclerview-v7:27.0.2'
    api 'com.android.support:support-v13:27.0.2'
    api 'com.google.android.gms:play-services-safetynet:11.8.0'
    api(name: 'setup-wizard-lib-platform-release', ext: 'aar')
    implementation project(':PiLib')
    compile 'com.google.code.gson:gson:2.8.2'

    testImplementation 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:' + rootProject.supportLibVersion
    androidTestCompile 'com.android.support.test:runner:' + rootProject.runnerVersion
    androidTestCompile 'com.android.support.test:rules:' + rootProject.rulesVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion

}
